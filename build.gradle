plugins {
    id "java"
}

group = "com.martinvana.aoc2024"
version = "1.0-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:5.11.3")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += '--enable-preview'
    jvmArgs += '-ea'
}

tasks.withType(JavaExec).configureEach { javaExecTask ->
    javaExecTask.doFirst {
        javaExecTask.extensions.extraProperties["startTime"] = System.nanoTime()
    }
    javaExecTask.doLast {
        def startTime = javaExecTask.extensions.extraProperties["startTime"]
        def durationMillis = (System.nanoTime() - startTime) / 1_000_000
        println "Task ${javaExecTask.path} took ${durationMillis} ms"
    }
}